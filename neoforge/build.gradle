//file:noinspection GrDeprecatedAPIUsage
//file:noinspection GroovyAssignabilityCheck
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

version rootProject.version

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url = "https://maven.neoforged.net/releases" }
}

dependencies {
    neoForge "net.neoforged:neoforge:$neoforge_version"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-neoforge:$architectury_version"

    modApi "com.ultreon.mods:ultreonlib-neoforge:$ultreonlib_version"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    include forgeRuntimeLibrary(implementation("io.github.spair:imgui-java-binding:$imgui_version"))
    include forgeRuntimeLibrary(implementation("io.github.spair:imgui-java-lwjgl3:$imgui_version"))
    include forgeRuntimeLibrary(implementation("io.github.spair:imgui-java-natives-linux:$imgui_version"))
    include forgeRuntimeLibrary(implementation("io.github.spair:imgui-java-natives-macos:$imgui_version"))
    include forgeRuntimeLibrary(implementation("io.github.spair:imgui-java-natives-windows:$imgui_version"))

    forgeRuntimeLibrary "com.github.Ultreon:json5-api:$json5_api_version"
    forgeRuntimeLibrary "io.github.ultreon.corelibs:commons-v0:$corelibs_version"
    forgeRuntimeLibrary "io.github.ultreon.corelibs:collections-v0:$corelibs_version"
    forgeRuntimeLibrary "io.github.ultreon.corelibs:registries-v0:$corelibs_version"
}

processResources {
    inputs.property "version", rootProject.version
    inputs.property "description", rootProject.getModDescription()
    System.out.println(inputs.getProperties())

    filesMatching("META-INF/mods.toml") {
        HashMap<String, ?> p = new HashMap<>()
        p.put("version", rootProject.version)
        p.put("description", rootProject.getModDescription())
        p.put("loaderVersion", rootProject.neoforge_version.split("\\.")[0])
        p.put("neoforgeVersion", rootProject.neoforge_version)
        p.put("minecraftVersion", rootProject.architectury.minecraft)
        expand(p)
    }

    from "$rootProject.projectDir/LICENSE"
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
    manifest {
        attributes([
                "Specification-Title"     : "Ultreon Modding Lib",
                "Specification-Vendor"    : "Ultreon Team",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Ultreon Team",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        mavenLocal()
    }
}

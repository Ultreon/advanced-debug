//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id "io.freefair.lombok" version "6.4.1"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = 'com.ultreon.mods'
version = '1.1.2-dev1'

java {
    archivesBaseName = 'advanced-debug'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

Project proj = project

eclipse {
    classpath {
        file {
            whenMerged { cp ->
                project.logger.lifecycle "[eclipse] Excluding sourceSet outputs from eclipse dependencies for project '${project.path}'"
                cp.entries.grep { it.kind == 'lib' }.each { entry ->
                    rootProject.allprojects { Project project ->
                        String buildDirPath = project.buildDir.path.replace('\\', '/') + '/'
                        String entryPath = entry.path

                        if (entryPath.startsWith(buildDirPath)) {
                            cp.entries.remove entry

                            if (project != proj) {
                                boolean projectContainsProjectDep = false
                                for (Configuration cfg : proj.configurations) {
                                    boolean cfgContainsProjectDependency = cfg.allDependencies.withType(ProjectDependency).collect { it.dependencyProject }.contains(project)
                                    if(cfgContainsProjectDependency) {
                                        projectContainsProjectDep = true
                                        break
                                    }
                                }
                                if (!projectContainsProjectDep) {
                                    throw new GradleException("The project '${proj.path}' has a dependency to the outputs of project '${project.path}', but not to the project itself. This is not allowed because it will cause compilation in eclipse to behave differently than in gradle.")
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

sourceSets {
    // Note that just declaring this source set creates two configurations.
    api {
        java {

        }
    }
    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}

minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly.
    // stable     #          Stables are built at the discretion of the MCP team.
    // official   MCVersion  Official field/method names from Mojang mapping files
    //
    // You must be aware of the Mojang license when using the 'official' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: '1.18.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run/client')

            ideaModule "${rootProject.name}.main"

            // Recommended logging data for a user-dev environment
            // The markers can be added/removed as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                advanceddebug {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            ideaModule "${rootProject.name}.main"

            // Recommended logging data for a user-dev environment
            // The markers can be added/removed as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                advanceddebug {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            ideaModule "${rootProject.name}.main"

            // Recommended logging data for a user-dev environment
            // The markers can be added/removed as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'advanceddebug', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                advanceddebug {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "advanced-debug.refmap.json"

    config "advanced-debug.mixins.json"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }
}

configurations {
   apiImplementation.extendsFrom(implementation)
   apiCompileOnly.extendsFrom(compileOnly)
   apiRuntimeOnly.extendsFrom(runtimeOnly)
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The user-dev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.18.1-39.1.0'

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "advanceddebug",
                "Specification-Vendor"    : "Qboi123",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Qboi123",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    dependsOn "apiJar"
}

classes {
    dependsOn "apiClasses"

    doLast {
        // Add directories with generated sources (might be several - one for every supported language).
        copy {
            from sourceSets.api.output.classesDirs
            into sourceSets.main.output.classesDirs.singleFile
            duplicatesStrategy DuplicatesStrategy.EXCLUDE
        }
    }
}

processResources {
    dependsOn "processApiResources"

    // Add output directories of the source set's resources.
    from sourceSets.api.output.resourcesDir
}

// Example for how to get properties into the manifest for reading at runtime.
task apiJar (type: Jar) {
    manifest {
        attributes([
                "Specification-Title"     : "advanceddebug",
                "Specification-Vendor"    : "Qboi123",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Qboi123",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // Add directories with generated sources (might be several - one for every supported language).
    from sourceSets.api.output.classesDirs

    // Add output directories of the source set's resources.
    from sourceSets.api.output.resourcesDir

    classifier "api"
}

reobf {
    jar {

    }

    apiJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}

artifacts {
    archives apiJar
    archives jar
}

jar.finalizedBy('reobfJar')

publishing {
    repositories {
        maven {
            name = "Main"
            url = uri("${projectDir}/.maven_repo")
        }
        mavenLocal()
    }
    publications {
        //noinspection GroovyAssignabilityCheck
        main(MavenPublication) {
            from components.java
            artifact apiJar
            pom.withXml {
                //noinspection GroovyImplicitNullArgumentCall
                def pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.groupId.text() == 'net.minecraftforge' && it.artifactId.text() == "forge"
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
